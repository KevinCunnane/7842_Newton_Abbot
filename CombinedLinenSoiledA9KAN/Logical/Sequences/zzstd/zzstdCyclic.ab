PROGRAM _CYCLIC
	(* cyclic program *)

;Version 1.00 First Issue
;Version 2.00 Added Bag[0] and Removed Mitsubishi M90xx's clock's

;Set all of bag 0 to Zero for use when deleting bags
Bag[0].Cat = 0
Bag[0].Dst = 0
Bag[0].Wgt = 0
Bag[0].Cnt = 0
Bag[0].Cus = 0
Bag[0].Day = 0
Bag[0].Spare = 0

;Standard Clocks
TimerClkPT[0] = 5 	;100ms
TimerClkPT[1] = 10 	;200ms
TimerClkPT[2] = 25 	;500ms
TimerClkPT[3] = 50 	;1s
TimerClkPT[4] = 100 ;2s

LOOP n = 0 TO 4 DO

	TimerClkA[n].IN = NOT TimerClkB[n].Q
	TimerClkA[n].PT = TimerClkPT[n]
	TimerClkA[n] FUB TON_10ms()

	TimerClkB[n].IN = TimerClkA[n].Q
	TimerClkB[n].PT = TimerClkPT[n]
	TimerClkB[n] FUB TON_10ms()

ENDLOOP

Std.Clock100ms 	= TimerClkA[0].Q
Std.Clock200ms 	= TimerClkA[1].Q ;M9031
Std.Clock500ms 	= TimerClkA[2].Q
Std.Clock1s 	= TimerClkA[3].Q ;M9032
Std.Clock2s 	= TimerClkA[4].Q ;M9033
END_PROGRAM

