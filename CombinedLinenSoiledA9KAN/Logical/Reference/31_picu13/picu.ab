PROGRAM _CYCLIC

;Sequence 24 - Primary Incline Conveyor 1 D[900 + mySeq]

;Note - Not a real sequence and not shown

;-----------------------------------------
;Revision 1.00 17/04/2007 D.J. First Issue
;-----------------------------------------

mySeq = 31	
myIO = 172
myX20IO	= 23	
myBufferBag = 841
myRelLine	= 65	
myLine = 82 ; Line used to track bags	
myZone	= 4
	
myLastBag = Line[myLine].LastBag	
myFirstBag = Line[myLine].FirstBag	
myRelBag = Line[myRelLine].LastBag
	
I_PaddleInPosition		= X[myIO].X01
I_BagReleasedSep		= X[myIO].X02
I_BagAtSep              = X[myIO].X03
I_ArriveIncline			= X[myIO].X04

O_RunMotor 				= FALSE
O_OpenSeparator			= FALSE
	
Alarm[mySeq].Name 	= 0
Alarm[mySeq].Bag	= myFirstBag
Alarm[mySeq].Beacon	= TRUE
Alarm[mySeq].Extra	= 0
Alarm[mySeq].Time	= 3000

;Setup Sequence
;-----------------------
STEP = D[900 + mySeq]
;Setup Pulse contact
PLS = (STEP <> LASTSTEP)
LASTSTEP = STEP

;If no bags on the buffers then stop the motor
NoBags = (Bag[myBufferBag].Cat = 0) AND (Bag[myFirstBag].Cat = 0)
TON_10ms(NoBags,10,StopIncline,StopInclineET)

O_RunMotor = (Line[myRelLine].Clear AND NOT Line[myRelLine].TripFault AND NOT StopIncline AND NOT Std.HaltSysZone[myZone] AND (STEP <> 2) AND (STEP <> 0))
IF (STEP = 0) THEN

ENDIF

;Run Incline until paddle in position
TON_10ms(I_BagAtSep,250,SettledInSep,SettledInSepET)
TON_10ms((Bag[myBufferBag].Cat > 0) AND NOT Std.HaltSysZone[myZone],300,SettledAtSep,SettledAtSepET)	
IF (STEP = 1) THEN
	IF (I_PaddleInPosition AND O_RunMotor AND SettledAtSep AND (Bag[myLastBag].Cat = 0) AND (Bag[myBufferBag].Cat > 0)) THEN
		D[900 + mySeq] = 2
	ENDIF
ENDIF

;Release Bag into incline	
IF (STEP = 2) THEN
	IF SettledInSep THEN
		BagArrived = TRUE
	ENDIF		
	O_OpenSeparator	= (Bag[myLastBag].Cat = 0) AND NOT BagArrived

	IF I_ArriveIncline THEN
		IF ((Bag[myBufferBag].Cat > 0) AND (Bag[myLastBag].Cat = 0)) THEN
			Bag[myLastBag] = Bag[myBufferBag]
			Bag[myBufferBag] = Bag[0]
		ENDIF
		D[900 + mySeq] = 3
	ENDIF	
	IF (Bag[myLastBag].Cat > 0) THEN
		Alarm[mySeq].Name = almBagDataError
		Alarm[mySeq].Time = 0
		Alarm[mySeq].Bag = myLastBag
	ELSE IF (Bag[myBufferBag].Cat > 0) AND NOT I_BagAtSep THEN 
		Alarm[mySeq].Name = almSwitchFault	
		Alarm[mySeq].Bag = myBufferBag	
	ELSE		
		Alarm[mySeq].Name = almBagNotReleased		
		Alarm[mySeq].Bag = myBufferBag
	ENDIF		
ELSE
	BagArrived = FALSE		
ENDIF

;Check not stpped on paddle
IF (STEP = 3) THEN
	IF NOT I_PaddleInPosition THEN
		D[900 + mySeq] = 1
	ENDIF
ENDIF

;Move data from incline..
IF Line[myRelLine].Trip AND (Bag[myFirstBag].Cat > 0) AND (Bag[myRelBag].Cat = 0)  THEN ; Trip includes debounce
	Bag[myRelBag] = Bag[myFirstBag]		
	Bag[myFirstBag] = Bag[0]	
ENDIF	

;Alarms
TimerAlarm.IN 		= NOT PLS AND (Alarm[mySeq].Name > 0)
TimerAlarm.PT 		= Alarm[mySeq].Time
TimerAlarm FUB TON_10ms()
Alarm[mySeq].On 	= TimerAlarm.Q
Alarm[mySeq].ET 	= TimerAlarm.ET
Alarm[mySeq].Zone 	= myZone
	
Y[myX20IO].Y02 	= O_RunMotor
Y[myIO].Y06 	= O_OpenSeparator AND NOT OpenSep
END_PROGRAM

